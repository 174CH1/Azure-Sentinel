// Title:           Symantec ProxySG Data Parser
// Author:          Microsoft
// Version:         1.0
// Last Updated:    05/21/2020
// Comment:         Inital Release
//
// DESCRIPTION:
// This parser takes raw Symantec ProxySG Access Logs from a Syslog data stream and parses the data into a normalized schema
//
// REFERENCES: 
// Using functions in Azure monitor log queries: https:docs.microsoft.com/azure/azure-monitor/log-query/functions
// Symantec ProxySG Access log format: https:portal.threatpulse.com/docs/sol/Solutions/Reference/accesslogformats-ref.htm
//
// LOG SAMPLES:
// This parser assumes the raw log are formatted as follows:
// 
//      2020-05-01 23:07:37 628 10.0.0.0 user67 - - OBSERVED "Web Ads/Analytics" -  200 TCP_TUNNELED CONNECT - tcp gmtdmp.mookie1.com 443 / - - "Mozilla/5.0 (Windows NT 6.1; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/81.0.4044.129 Safari/537.36" 10.10.10.10 1601 1586 - "none" "none" 3128 Ambiguous%20-%20Special%20Use 2
//      2020-05-01 23:07:33 2765 10.1.1.1 user9 - - OBSERVED "WhiteList;Web Ads/Analytics" -  200 TCP_TUNNELED CONNECT - tcp pagead2.googlesyndication.com 443 / - - "Mozilla/5.0 (Windows NT 6.1; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/81.0.4044.129 Safari/537.36" 10.10.10.10 4269 1474 - "none" "none" 3128 Ambiguous%20-%20Special%20Use 2
//
//
Syslog
| where Computer in ("datasource") and Facility == "local0"
| parse SyslogMessage with logTime:datetime " " time_taken:long " " c_ip:string " " cs_userdn:string " " cs_auth_groups:string " " exception_id " " sc_filter_result:string ' "' cs_categories:string '" "' cs_referrer:string '" ' sc_status:string " " s_action:string " " cs_method:string ' "' content_type:string '" ' cs_uri_scheme:string " " cs_host:string " " cs_uri_port:string " " cs_uri_path:string " " cs_uri_query:string " " cs_uri_extension:string ' '  Part2:string
| extend cs_categories = split(cs_categories,";") 
| extend content_type = split(replace(@"%20",@'',tostring(content_type)),";")
| parse Part2 with '"' UserAgent '" ' Part3
| extend cs_user_agent = iff(Part2 startswith "-", "-", UserAgent),
         Part3 = iff(Part2 startswith "-", substring(Part2,2), Part3)
| parse Part3 with s_ip:string " " sent_bytes:long " " received_bytes:long " " virus_id:string  ' "' app_name:string  '" "' app_operation:string '" ' src_port:string ' "' country:string '" ' cs_threat_risk:string "#015"
| project-away Part2, Part3, UserAgent
